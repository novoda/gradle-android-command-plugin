buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.10.+'
        // To depend on the deployed version of the plugin add the following line here:
//        classpath 'com.novoda:gradle-android-command-plugin:1.2.1'
    }
}
apply plugin: 'android'
apply plugin: 'android-command'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.3'

    defaultConfig {
        versionCode 1
        versionName 'v0.0.1'

        minSdkVersion 14
        targetSdkVersion 19
    }

    buildTypes {
        debug {}
        release {}
        QA {}
    }

    productFlavors {

        flavorDimensions "pricing", "releaseType"

        beta {
            flavorDimension "releaseType"
        }

        normal {
            flavorDimension "releaseType"
        }

        free {
            flavorDimension "pricing"
        }

        paid {
            flavorDimension "pricing"
        }
    }

    // change APK name to include the version name
    applicationVariants.all { variant ->
        def file = variant.packageApplication.outputFile
        variant.packageApplication.outputFile = new File(file.parent, file.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
    }

    command {
        deviceId {
            def minSdkDevices = devices().grep({ it.sdkVersion() >= defaultConfig.minSdkVersion }).sort()
            if (!minSdkDevices) {
                throw new IllegalStateException('No device found running android version >= ' + defaultConfig.minSdkVersion)
            }
            minSdkDevices[0].id
        }

        events 1000
        task('instHudl', com.novoda.gradle.command.Install) {
            device {
                def hudlDevices = devices().grep({ it.brand() == 'hudl' })
                if (!hudlDevices) {
                    throw new GroovyRuntimeException('No hudl device found')
                }
                hudlDevices[0].id
            }
        }
        task('installNewest', com.novoda.gradle.command.Run) {
            device {
                def device = devices().max({ it.sdkVersion() })
                if (!device) {
                    throw new GroovyRuntimeException('No device found')
                }
                device
            }

        }

        task('runNewest', com.novoda.gradle.command.Run, ['installDevices']) {
            device {
                def device = devices().max({ it.sdkVersion() })
                if (!device) {
                    throw new GroovyRuntimeException('No device found')
                }
                device
            }
        }

        task('autoLogin', type: com.novoda.gradle.command.Input) {
            script {
                2.times {
                    text 'bob'
                    enter()
                }
                enter()
            }
        }

        task('listDevices') << {
            println()
            println "Attached devices:"
            android.command.devices().grep { it.sdkVersion() >= 14 }.each {
                println(it.manufacturer() + " " + it.model())
            }
        }
        sortBySubtasks false
    }
}
